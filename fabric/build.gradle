plugins {
    id 'java'
    id 'idea'
    id 'fabric-loom' version '1.6.+'
}
base {
    archivesName = "${mod_id}-fabric-mc${minecraft_version}-${mod_version}"
}
repositories {
    maven { url "https://jitpack.io" }
    maven { url "https://maven.terraformersmc.com/" }
}
dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered {
        it.officialMojangMappings()
        it.parchment("org.parchmentmc.data:parchment-${project.parchment_mc_version}:${project.parchment_version}@zip")
    }
    modImplementation("net.fabricmc:fabric-loader:${project.fabric_loader_version}")
    include(implementation("com.h2database:h2:${project.h2_version}"))
    modCompileOnly("com.terraformersmc:modmenu:${project.modmenu_version}")
    implementation project(":common")
}
loom {
    accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.aw"))
    mixin {
        useLegacyMixinAp = false
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}
tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}
processResources {
    from project(":common").sourceSets.main.resources
}